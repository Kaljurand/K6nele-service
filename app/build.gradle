apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
    implementation project(':speechutils:app')
    implementation 'com.koushikdutta.async:androidasync:3.1.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    // implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'com.google.android.material:material:1.11.0'
}

android {
    compileSdk rootProject.compileSdk

    // API level 7: MediaRecorder.AudioSource.VOICE_RECOGNITION
    // API level 8: android.speech.SpeechRecognizer and android.speech.RecognitionService
    // API level 14: @android:style/Theme.DeviceDefault
    defaultConfig {
        applicationId 'ee.ioc.phon.android.k6neleservice'
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 210
        versionName '0.2.10'
        vectorDrawables.useSupportLibrary = true
        // Keep only en and et resources
        resConfigs 'en', 'et'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release {}
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
    }

    lint {
        // TODO: in the future check for Kotlin-Java interop
        //check 'Interoperability'
        disable 'ResourceType', 'AppLinkUrlError', 'EllipsizeMaxLines', 'RtlSymmetry', 'Autofill'
    }
    namespace 'ee.ioc.phon.android.k6neleservice'

}


if (project.hasProperty('storeFilename') && project.hasProperty('storePassword') && project.hasProperty('keyAlias') && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFilename)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    println "WARNING: The release will not be signed"
    android.buildTypes.release.signingConfig = null
}


tasks.register('deploy') {
    doLast {
        description 'Copy the APK and the ProGuard mapping file to the deploy directory'

        def deploy_dir = System.getenv('APK_DEPLOY_DIR')

        def version = android.defaultConfig.versionName

        def name = 'K6nele-service'

        def outputs = 'build/outputs/'
        def apk1 = outputs + 'apk/release/app-release.apk'
        def apk2 = "${deploy_dir}${name}-${version}.apk"
        def mapping1 = outputs + 'mapping/release/mapping.txt'
        def mapping2 = "${deploy_dir}${name}-mapping-${version}.txt"

        exec {
            commandLine 'cp', '--verbose', apk1, apk2
        }

        exec {
            commandLine 'cp', '--verbose', mapping1, mapping2
        }

        exec {
            commandLine 'ls', '-l', deploy_dir
        }

        println "adb uninstall ${android.defaultConfig.applicationId}"
        println "adb install ${apk2}"
    }
}
